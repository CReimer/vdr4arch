# Maintainer: Manuel Reimer <manuel.reimer@gmx.de>
# Maintainer: Tomasz Maciej Nowak <com[dot]gmail[at]tmn505>

pkgname='minisatip'
pkgdesc='SAT>IP server, tested with DVB-S, DVB-S2, DVB-T, DVB-T2, DVB-C, DVB-C2, ATSC and ISDB-T cards'
pkgver=1.0.0
pkgrel=4
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h' 'aarch64')
url='https://minisatip.org'
license=('GPL2')
conflicts=('minisatip-git')
depends=('libdvbcsa' 'linuxtv-dvb-apps' 'openssl' 'libmcli')
optdepends=('oscam: channels descrambling')
backup=('etc/conf.d/minisatip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/catalinii/minisatip/archive/${pkgver}.tar.gz"
        'fix-netceiver-build.patch::https://github.com/catalinii/minisatip/commit/ef8848ea.patch'
        'minisatip-netceiver-api-socket.patch'
        'minisatip.service'
        'minisatip.sysuser'
        'minisatip.conf')
sha256sums=('e6d477a0293a7383047a6404b628dfed7e7a3c6cee3f720ec024da3856794f71'
            '62f184e2bc119d08a66ac7b12e89cc31f3a95820493bfbbadd882d67902924a3'
            '161303bed5df7721621cef0f36f92fd2bce06539f5c7a68c26cc24b05a8a46b4'
            'e35a56f32aa3ac22b4b4d6e959e47d07268a6bec0fe8a9b0bfd8f87c89949f00'
            '7f4e7fde7ded632f88b30b7cd0481c78309f8191b40369ae323cbb7240fdc199'
            'f7a28ad30d30c68ef54ba65cc562ce0d9a131f0ee2be20071cd93159f196946f')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/fix-netceiver-build.patch"
  patch -p1 -i "${srcdir}/minisatip-netceiver-api-socket.patch"
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --with-mcli=/usr/include/libmcli
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -Dm644 ${srcdir}/minisatip.service ${pkgdir}/usr/lib/systemd/system/minisatip.service
  install -Dm644 ${srcdir}/minisatip.sysuser ${pkgdir}/usr/lib/sysusers.d/minisatip.conf
  install -Dm644 ${srcdir}/minisatip.conf ${pkgdir}/etc/conf.d/minisatip
  install -Dm755 minisatip ${pkgdir}/usr/bin/minisatip
  mkdir -p "${pkgdir}/usr/share/minisatip/html"
  cp -r html "${pkgdir}/usr/share/minisatip"
}
