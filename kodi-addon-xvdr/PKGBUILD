# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=kodi-addon-xvdr
pkgver=0.9.8_57_g82b5093
_gitver=328fa653b821a4edc1256a13833f30a4483e2819
pkgrel=1
url="https://github.com/pipelka/xbmc-addon-xvdr"
arch=('x86_64' 'i686')
license=('GPL2')
pkgdesc="PVR add-on for XBMC to add VDR as a TV/PVR Backend"
replaces=('xbmc-addon-xvdr')
conflicts=('xbmc-addon-xvdr')
depends=('gcc-libs' 'kodi' 'zlib')
makedepends=('git')
source=("git+https://github.com/pipelka/xbmc-addon-xvdr.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/xbmc-addon-xvdr"
  git describe --tags | sed 's/xvdr-//g;s/-/_/g'
}

build() {
  cd "${srcdir}/xbmc-addon-xvdr"
  ./autogen.sh

  ./configure --prefix=/usr/lib/kodi

  make
}

package() {
  cd "${srcdir}/xbmc-addon-xvdr"
  make DESTDIR="${pkgdir}" install
}