# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
_srcname=anthra_1920_FS
pkgname=text2skin-$(echo ${_srcname//_/-} | tr '[:upper:]' '[:lower:]')
pkgver=0.1.0
pkgrel=2
pkgdesc="Dark, anthracite skins for Klaus Schmidingers VDR"
arch=('any')
url="http://www.anthra.de/"
depends=('vdr-text2skin')
optdepends=('anthra-logos: To show channellogos in DisplayChannel view')
groups=('text2skin-anthra')
license=('GPL2')
install=$pkgname.install
source=("http://www.anthra.de/anthra-skins/$_srcname-$pkgver.tar.xz")
md5sums=('ebb1d7c3ce37c6dee0abf99b2828d01d')

prepare() {
  cd "$srcdir/$_srcname"
  sed -i 's/var\/lib\/vdr/usr\/share\/vdr\/plugins\/text2skin/g' $_srcname.skin
}

package() {
  cd "$srcdir/$_srcname"
  make DESTDIR="$pkgdir" THEMESDIR="/var/lib/vdr/themes" install
  find "$pkgdir" -name COPYING -delete
  find "$pkgdir" -name .directory -delete
}
