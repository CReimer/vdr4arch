pkgbase=channellogos
pkgname=({anthra,elchi,enigma,narrowhd,nopacity}-logos)
pkgver=20140908
pkgrel=1
pkgdesc="Channellogos for VDR"
arch=('any')
url="http://creimer.net/channellogos/"
license=('GPL2')
source=(http://creimer.net/channellogos/{anthra,elchi-{576,720,1080},enigma,narrowhd,nopacity-{black,white}}-$pkgver.tar.bz2)
options=(!strip)
md5sums=('3da3fb2aee4b9220ccfbc43842dc2684'
         'edfeeef79f48b0ca529413e4c150c7ba'
         'e84648dc8d423b4e3656dfee0443859c'
         '1be2b704dee591690de0dacfabba2b6e'
         'b65e440c3c8588a0708a40ce6ba6227c'
         '090c67aafd7d30c27c5f79f376d77c6f'
         '50873415496e34429dfe2d95cfd6b489'
         'b98324077e58214cdda966e7f94fa19e')

package_anthra-logos() {
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/text2skin/anthralogos"
  cp -r anthra/* "$pkgdir/usr/share/vdr/plugins/text2skin/anthralogos"
}

package_elchi-logos() {
  cd "$srcdir"
  mkdir -p "$pkgdir"/usr/share/vdr/plugins/skinelchi/logos/{576,720,1080}
  cp -r elchi-576/* "$pkgdir/usr/share/vdr/plugins/skinelchi/logos/576"
  cp -r elchi-720/* "$pkgdir/usr/share/vdr/plugins/skinelchi/logos/720"
  cp -r elchi-1080/* "$pkgdir/usr/share/vdr/plugins/skinelchi/logos/1080"
}

package_enigma-logos() {
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skinenigmang/logos"
  cp -r enigma/* "$pkgdir/usr/share/vdr/plugins/skinenigmang/logos"
}

package_narrowhd-logos() {
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/text2skin/narrowhdlogos"
  cp -r narrowhd/* "$pkgdir/usr/share/vdr/plugins/text2skin/narrowhdlogos"
}

package_nopacity-logos() {
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skinnopacity/logos"
  cd "$pkgdir/usr/share/vdr/plugins/skinnopacity/logos"
  cp -r "$srcdir/nopacity-white" white
  cp -r "$srcdir"/nopacity-black/* .

  ln -s white blue
  ln -s white darkblue
  ln -s white darkred
  ln -s white darkredNG
  ln -s white green

  mkdir -p "$pkgdir/usr/share/vdr/plugins/tvguide"
  ln -srT "$pkgdir/usr/share/vdr/plugins/skinnopacity/logos" \
    "$pkgdir/usr/share/vdr/plugins/tvguide/logos"

  mkdir -p "$pkgdir/usr/share/vdr/plugins/skinflat"
  ln -srT "$pkgdir/usr/share/vdr/plugins/skinnopacity/logos/white" \
    "$pkgdir/usr/share/vdr/plugins/skinflat/logos"

  mkdir -p "$pkgdir/usr/share/vdr/plugins/skinflatplus"
  ln -srT "$pkgdir/usr/share/vdr/plugins/skinnopacity/logos/white" \
    "$pkgdir/usr/share/vdr/plugins/skinflatplus/logos"
}
