# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr-dvbapi
pkgver=20121115
_gitver=e87e15fc
_vdrapi=1.7.34
pkgrel=3
pkgdesc="A bridge between VDR and OScam."
url="https://github.com/manio/vdr-plugin-dvbapi"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' 'oscam' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
replaces=("vdr-plugin-$_plugname")
conflicts=("vdr-plugin-$_plugname")
source=("https://github.com/manio/vdr-plugin-$_plugname/archive/$_gitver.tar.gz")
md5sums=('b3a1f375d7cc09cdac21c6617fe10614')

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${_gitver}"*

  if [ "${CARCH}" = "x86_64" ]; then
    CPUOPT='x86-64'
    PARALLEL=PARALLEL_128_SSE2
  else
    CPUOPT="${CARCH}"
    PARALLEL=PARALLEL_32_INT
  fi

  mkdir -p $pkgdir/usr/lib/vdr/plugins
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       CPUOPT="$CPUOPT" \
       PARALLEL="$PARALLEL" \
       CSAFLAGS="$(pkg-config vdr --variable=cflags) -funroll-loops"
}
