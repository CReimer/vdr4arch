# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-softhddevice
pkgver=0.6.0_8_g9dd248f
epoch=1
_gitver=9dd248f9d0ad9df9cc3413b2ba92dcbc03aa8fd3
_vdrapi=2.0.0
pkgrel=1
pkgdesc="software and GPU emulated HD output device plugin for VDR"
url="http://projects.vdr-developer.org/projects/plg-softhddevice"
arch=('x86_64' 'i686')
license=('AGPL3')
depends=('ffmpeg' "vdr-api=${_vdrapi}" 'xcb-util-wm')
optdepends=('nvidia: Required for VDPAU acceleration')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-softhddevice.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"

#  sed -i 's/# -DDEBUG/-DDEBUG/g' Makefile
#  sed -i 's/-DAV_INFO //g' Makefile
  sed -i 's/#CONFIG += -DUSE_VDR_SPU/CONFIG += -DUSE_VDR_SPU/g' Makefile
  sed -i 's/#CONFIG += -DUSE_MPEG_COMPLETE/CONFIG += -DUSE_MPEG_COMPLETE/g' Makefile
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install
}
