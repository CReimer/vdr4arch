# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-xvdr
pkgver=0.9.9_29_g00bb444
_gitver=00bb4449c679f2e5f8be467cbed9665dbd32085e
_vdrapi=2.0.0
pkgrel=2
url="https://github.com/pipelka/vdr-plugin-xvdr"
arch=('x86_64' 'i686')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('vdr-wirbelscan: Support channel searches with XBMC')
makedepends=('git')
source=("git+https://github.com/pipelka/vdr-plugin-xvdr.git#commit=$_gitver"
        'xvdr-vdr2.1.2compat.diff')
backup=('var/lib/vdr/plugins/xvdr/allowed_hosts.conf'
        'var/lib/vdr/plugins/xvdr/xvdr.conf')
install="$pkgname.install"
md5sums=('SKIP'
         '3b77572e50ba076be127c85a0158838d')

pkgver() {
  cd "${srcdir}/vdr-plugin-xvdr"
  git describe --tags | sed 's/xvdr-//g;s/-/_/g'
}

prepare() {
  cd "${srcdir}/vdr-plugin-xvdr"
  patch -p1 -i "$srcdir/xvdr-vdr2.1.2compat.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-xvdr"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-xvdr"
  make DESTDIR="${pkgdir}" install install-conf
}
