# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-scraper2vdr
pkgver=0.1.6
_gitver=02013accff3db6f3da151440473ecf5fb74be83b
_vdrapi=2.1.9
pkgrel=4
pkgdesc="Acts as client and provides scraped metadata for tvshows and movies from epgd"
url="http://projects.vdr-developer.org/projects/plg-scraper2vdr"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' 'libmariadbclient' "vdr-api=${_vdrapi}")
makedepends=('git' 'imlib2')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-scraper2vdr.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         'b3be8ea56e120b1b0e61d9f53cd9dc26')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git tag -a 0.1.6 -m 'Added Tag' 02013accff3db6f3da151440473ecf5fb74be83b 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make -j1 IMAGELIB=graphicsmagick
}

package() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" IMAGELIB=graphicsmagick install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
