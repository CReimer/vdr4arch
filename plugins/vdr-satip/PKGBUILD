# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-satip
pkgver=1.0.1
_vdrapi=2.1.7
pkgrel=2
pkgdesc="Integrates SAT>IP network devices seamlessly into VDR"
url="http://www.saunalahti.fi/rahrenbe/vdr/satip/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'gcc-libs' 'tinyxml' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://www.saunalahti.fi/~rahrenbe/vdr/satip/files/$pkgname-$pkgver.tgz"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
options=('!emptydirs')
md5sums=('1fe9950b5f8b6b35c57ee5485cb04484'
         '4a4b93779b692ee9023983cf2515be90')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make GITTAG='' SATIP_USE_TINYXML=1
}


package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
