# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-plex
pkgver=0.0.1_4_g0b1771d
_gitver=0b1771db632f525a68b18b9d57b93c9e843074bc
_vdrapi=2.1.6
pkgrel=1
pkgdesc="Plex plugin for Linux VDR"
url="https://github.com/chriszero/vdr-plugin-plex"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('mplayer' 'poco' "vdr-api=${_vdrapi}" 'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-wm')
makedepends=('git')
replaces=('vdr-boblight')
conflicts=('vdr-boblight')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://github.com/chriszero/vdr-plugin-plex.git#commit=$_gitver")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git tag -a 0.0.1 -m 'Added Tag' a40adaf76fb1267d38b4c5e6386933ddb2d0d328 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
