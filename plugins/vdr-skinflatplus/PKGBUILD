# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinflatplus
pkgver=0.0.1_8_gdf0ef5f
_gitver=df0ef5f977678ca3dc2564cfb50457c5dc0e7b9d
_vdrapi=2.0.0
pkgrel=1
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflat"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-skin//g')
source=("git://projects.vdr-developer.org/skin-flatplus.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-${_plugname}"
  git tag -a v0.0.1 -m 'Added Tag' b37329118c3fad7e5a286280d7d0a3ed47b562a7 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/skin-${_plugname}"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
}

build() {
  cd "${srcdir}/skin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/skin-${_plugname}"
  make DESTDIR="$pkgdir" install
}
