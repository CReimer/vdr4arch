# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skindesigner
pkgver=0.1.2
_gitver=2ddc1ee46bb066029d8977801972362d0daae4c2
_vdrapi=2.1.6
pkgrel=1
pkgdesc="Highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('Apache' 'GPL2')
depends=('librsvg' "vdr-api=${_vdrapi}")
makedepends=('git')
optdepends=('nopacity-logos: To show channellogos in DisplayChannel view'
            'ttf-opensans: Required if you want to use the metrixhd skin'
            'vdr-scraper2vdr: To display scraped images in various EPG views (requires epgd)'
            'vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-skindesigner.git#commit=$_gitver"
        'skindesigner-always_show_displaychannel.diff'
        'skindesigner-use-opensans-font.diff'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
options=('!emptydirs')
md5sums=('SKIP'
         'b7b96592d3c0d0fc1ac994dee8594ac5'
         '2bb84dd39119c2a673887d178dcb4839'
         'ebc6537bf0d3e604c59884ec408f7e9c')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  patch -p1 -i "$srcdir/skindesigner-always_show_displaychannel.diff"
  patch -p1 -i "$srcdir/skindesigner-use-opensans-font.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
