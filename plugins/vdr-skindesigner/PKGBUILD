# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase='vdr-skindesigner'
pkgname=($pkgbase skindesigner-{blackhole,metrixhd,nopacity})
pkgver=0.1.5
_gitver=3a6dced6c1e0f001bda8e45fc4879d361fefb6ed
_vdrapi=2.1.7
pkgrel=1
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('git' 'librsvg' 'vdr')
_plugname=$(echo $pkgbase | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-skindesigner.git#commit=$_gitver"
        'skindesigner-always_show_displaychannel.diff'
        'skindesigner-use-opensans-font.diff'
        "50-$_plugname.conf")
options=('!emptydirs')
md5sums=('SKIP'
         'b7b96592d3c0d0fc1ac994dee8594ac5'
         'd2dab86d07fc0fa38a4141d2e728c620'
         'ebc6537bf0d3e604c59884ec408f7e9c')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  patch -p1 -i "$srcdir/skindesigner-always_show_displaychannel.diff"
  patch -p1 -i "$srcdir/skindesigner-use-opensans-font.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make
}

package_vdr-skindesigner() {
  pkgdesc="Highly customizable native true color skin for the Video Disc Recorder"
  depends=('librsvg' "vdr-api=${_vdrapi}")
  optdepends=('vdr-scraper2vdr: To display scraped images in various EPG views (requires epgd)'
              'vdr-tvscraper: To display scraped images in various EPG views')
  backup=("etc/vdr/conf.avail/50-$_plugname.conf")

  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
  rm -r "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  rm -r "$pkgdir/var/lib/vdr/themes"

  cd "$pkgdir/usr/lib/vdr/plugins/skindesigner/scripts"
  mv temperatures.default temperatures
  rm README temperatures.*
}

package_skindesigner-blackhole() {
  arch=('any')
  pkgdesc="Blackhole Skin for SkinDesigner"
  depends=('ttf-sourcesanspro' 'vdr-skindesigner')

  cd "${srcdir}/vdr-plugin-$_plugname"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r skins/blackhole "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  cp -r themes/blackhole-* "$pkgdir/var/lib/vdr/themes"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}

package_skindesigner-metrixhd() {
  arch=('any')
  pkgdesc="MetrixHD Skin for SkinDesigner"
  depends=('ttf-opensans' 'vdr-skindesigner')

  cd "${srcdir}/vdr-plugin-$_plugname"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r skins/metrixhd "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
#   cp -r themes/metrixhd-* "$pkgdir/var/lib/vdr/themes"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}

package_skindesigner-nopacity() {
  arch=('any')
  pkgdesc="nOpacity Skin for SkinDesigner"
  depends=('vdr-skindesigner')

  cd "${srcdir}/vdr-plugin-$_plugname"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r skins/nopacity "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  cp -r themes/nopacity-* "$pkgdir/var/lib/vdr/themes"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
