# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-weatherforecast
pkgver=0.0.1
_vdrapi=2.1.6
pkgrel=1
pkgdesc="provides a weather forecast based on forecast.io data"
url="http://projects.vdr-developer.org/projects/plg-weatherforecast"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'jansson' "vdr-api=$_vdrapi")
# makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
source=("http://projects.vdr-developer.org/git/vdr-plugin-$_plugname.git/snapshot/vdr-plugin-$_plugname-$pkgver.tar.bz2")
md5sums=('5d823a99ba5f2cd8801349d65fd195b8')

build() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
