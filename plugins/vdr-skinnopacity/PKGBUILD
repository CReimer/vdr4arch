# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=1.0.0_2_g8af28d3
_gitver=8af28d3a9dcb7dfd9c90ef523952c1d0e74652c4
_vdrapi=2.1.2
pkgrel=2
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
backup=("var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-light.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-default.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-iceblue.conf")
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a v1.0.0 -m 'Added Tag' 96f22566023f2336e9bbcd2861f77ad97958014e 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/skin-nopacity"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "${srcdir}/skin-nopacity"

  make DESTDIR="$pkgdir" install

  find "$pkgdir" -name *.png -exec gm convert +profile icm {} {} \;
}
