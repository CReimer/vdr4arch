# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr-skinnopacity
pkgver=0.0.4
_vdrapi=1.7.33
pkgrel=2
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url=""
arch=('x86_64' 'i686')
license=('GPL2')
depends=('imagemagick' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
replaces=("vdr-plugin-$_plugname")
conflicts=("vdr-plugin-$_plugname")
source=("http://projects.vdr-developer.org/attachments/download/1146/$pkgname-$pkgver.tgz")
md5sums=('e0ca4ee6bbfd90ae50a10be879a2af0f')

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  sed -i '/Make\.global/d' Makefile

  mkdir -p $pkgdir/usr/lib/vdr/plugins
  make CFLAGS="$(pkg-config vdr --variable=plugincflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=plugincflags)" \
       VDRDIR="$(pkg-config vdr --variable=includedir)/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=plugindir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=localedir)"

  mkdir -p $pkgdir/usr/share/vdr/plugins/$_plugname/icons
  cp -r icons/* $pkgdir/usr/share/vdr/plugins/$_plugname/icons

  mkdir -p $pkgdir/var/lib/vdr/themes
  cp -r themes/* $pkgdir/var/lib/vdr/themes
}
