# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=0.1.0_22_ge269a87
_gitver=e269a87fc24964fbaac485d6a60f3c331ed321a6
_vdrapi=2.0.0
pkgrel=1
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('curl' 'imagemagick' 'libpng' 'libxml2' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver")
backup=("var/lib/vdr/plugins/$_plugname/rssfeeds.conf")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a v0.1.0 -m 'Added Tag' 1e0423cd81945c4cd9f922e66822c93f396aeb2b 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/skin-nopacity"
  make
}

package() {
  cd "${srcdir}/skin-nopacity"
  make DESTDIR="$pkgdir" install

  install -Dm644 conf/rssfeeds.conf "$pkgdir/var/lib/vdr/plugins/$_plugname/rssfeeds.conf"
}
