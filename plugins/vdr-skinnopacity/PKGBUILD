# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=1.0.2
_gitver=76ad37217ef27fee821a689f33251d21b9ed302a
_vdrapi=2.1.2
pkgrel=2
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
backup=("var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-light.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-default.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-iceblue.conf")
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a v1.0.2 -m 'Added Tag' 76ad37217ef27fee821a689f33251d21b9ed302a 2> /dev/null
  git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/v//g'
}

build() {
  cd "${srcdir}/skin-nopacity"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "${srcdir}/skin-nopacity"
  make DESTDIR="$pkgdir" install
}
