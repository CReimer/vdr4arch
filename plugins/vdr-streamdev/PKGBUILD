# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
# Contributor: Ole Ernst <olebowle[at]gmx[dot]com
pkgname=vdr-streamdev
pkgver=0.6.1_24_g7b17f77
_gitver=7b17f7725cd3bd97dd3921d96575c50a86e3fac2
epoch=1
_vdrapi=2.1.6
pkgrel=1
pkgdesc="implementation of the VTP (Video Transfer Protocol)"
url="http://projects.vdr-developer.org/projects/show/plg-streamdev"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
install="${pkgname}.install"
source=("git://projects.vdr-developer.org/vdr-plugin-streamdev.git#commit=$_gitver"
        "50-$_plugname-server.conf")
backup=("etc/vdr/conf.avail/50-$_plugname-client.conf"
        "etc/vdr/conf.avail/50-$_plugname-server.conf"
        'var/lib/vdr/plugins/streamdev-server/streamdevhosts.conf')
md5sums=('SKIP'
         '8c352d8ee7401eeb34fe5ab82e791a21')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git tag -a 0.6.1 -m 'Added Tag' 40704cdcbc012e15e9e814d8fe64303f13988f56 2> /dev/null
  git describe --tags | sed 's/-/_/g'
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" install

  install -Dm644 streamdev-server/streamdevhosts.conf "$pkgdir/var/lib/vdr/plugins/streamdev-server/streamdevhosts.conf"

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname-client]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname-client.conf"
  install -Dm644 "$srcdir/50-$_plugname-server.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname-server.conf"
}
