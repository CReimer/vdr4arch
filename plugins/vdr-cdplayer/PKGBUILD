# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-cdplayer
pkgver=1.1.8
_vdrapi=2.0.0
pkgrel=1
pkgdesc="CD-Player plugin which supports CD-Text and CDDB-Database"
url="http://uli-eckhardt.de/vdr/cdplayer.en.shtml"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libcdio-paranoia' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
backup=("var/lib/vdr/plugins/$_plugname/cd.mpg")
install="${pkgname}.install"
source=("http://www.uli-eckhardt.de/vdr/download/${pkgname}-${pkgver}.tgz")
md5sums=('8addea1683e9cb7ffc6196b51778e4f8')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  # DESTDIR!!!
  sed -i 's/$(CFGDIR)/$(DESTDIR)\/$(CFGDIR)/g' Makefile
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
