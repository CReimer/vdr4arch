# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-cecremote
pkgver=1.3.1
_vdrapi=2.3.1
pkgrel=2
pkgdesc="Receive and send commands via CEC"
url="http://uli-eckhardt.de/vdr"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libcec' 'pugixml' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("http://www.uli-eckhardt.de/vdr/download/${pkgname}-${pkgver}.tgz"
        'cecremote-vdr2.3.1compat.diff::http://git.uli-eckhardt.de/?p=vdr-plugin-cecremote.git;a=patch;h=601b1ea2911ebc7b60cdb2910c8e856aeb420be3'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('2d9980eea13edcebec5abaaf168fb0db'
         'f45ff0334c3db48b7394ed60ee951e23'
         'b07791e9ca361d41d58f2017fdcec70b')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/cecremote-vdr2.3.1compat.diff"
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  chown -R 666:666 "$pkgdir/var/lib/vdr"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
