# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-cecremote
pkgver=0.1.1
_vdrapi=2.2.0
pkgrel=2
pkgdesc="Receive and send commands via CEC"
url="http://uli-eckhardt.de/vdr"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libcec' "vdr-api=${_vdrapi}" 'xerces-c')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://www.uli-eckhardt.de/vdr/download/${pkgname}-${pkgver}.tgz"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('6901d582d70b4421a62f0a922ea4ce5f'
         'b07791e9ca361d41d58f2017fdcec70b')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 contrib/cecremote.xml "$pkgdir/var/lib/vdr/plugins/cecremote/cecremote.xml"
  chown -R 666:666 "$pkgdir/var/lib/vdr"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
