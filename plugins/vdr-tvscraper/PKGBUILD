# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-tvscraper
pkgver=0.0.5
_vdrapi=2.0.0
pkgrel=1
pkgdesc="Runs in the background and collects metadata for all available EPG events"
url="http://www.vdr-portal.de/board1-news/board2-vdr-news/119880-announce-tvscrapper-0-0-1/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('curl' 'jansson' 'libxml2' 'sqlite' "vdr-api=${_vdrapi}")
replaces=('vdr-tvscrapper')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/attachments/download/1454/$pkgname-$pkgver.tgz"
        'fix_null_pointer_access.diff::http://projects.vdr-developer.org/git/vdr-plugin-tvscraper.git/patch/?id=94a319d1a6ce1035a0d6187e414f2672a27b4ddf')
md5sums=('74a6c9ce0c9f9a517c9283691e5130d6'
         'a468a0fc49e31cffc6310587746fde28')

prepare() {
  cd "${srcdir}/$_plugname-$pkgver"
  patch -p1 -i "$srcdir/fix_null_pointer_access.diff"
}

build() {
  cd "${srcdir}/$_plugname-$pkgver"
  make
}

package() {
  cd "${srcdir}/$_plugname-$pkgver"
  make DESTDIR="$pkgdir" \
       PLGCONFDIR="$pkgdir/var/lib/vdr/plugins/tvscraper" \
       install
}
