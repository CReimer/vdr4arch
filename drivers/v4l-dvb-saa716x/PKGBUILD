# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=v4l-dvb-saa716x
pkgver=20141109
_hgver=d7e98fc592305a600909003da2b7cc4338242511
_kernver=3.17.6
url='http://powarman.dyndns.org/hgwebdir.cgi/v4l-dvb-saa716x'
pkgrel=1
pkgdesc="All interesting modules of powarman's repository"
arch=('x86_64' 'i686')
license=('GPL2')
depends=("linux=$_kernver")
makedepends=("linux-headers=$_kernver" 'mercurial')
install="${pkgname}.install"
replaces=('saa716x')
conflicts=('saa716x')
source=("hg+https://bitbucket.org/powARman/v4l-dvb-saa716x#revision=$_hgver"
        'compat.diff')
md5sums=('SKIP'
         '6ee3400dcbd4f445a0ae00c519782a42')

pkgver() {
  cd "$srcdir/$pkgname"
  hg log -r $_hgver --template "{shortdate(date)}" | tr -d '-'
}

prepare() {
  if ! grep -q "EXTRAMODULES=${_kernver%.*}" "$startdir/$pkgname.install"; then
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_kernver%.*}/g" "${startdir}/${pkgname}.install"
  fi

  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/compat.diff"
}

build() {
  cd "$srcdir/$pkgname/linux/drivers/media/common/saa716x"
  make -C /usr/lib/modules/$_kernver-*-ARCH/build M=$(pwd) CONFIG_SAA716X_CORE=m CONFIG_DVB_SAA716X_BUDGET=m \
    CONFIG_DVB_SAA716X_HYBRID=m CONFIG_DVB_SAA716X_FF=m modules
}


package() {
  mkdir -p "$pkgdir/usr/lib/modules/extramodules-${_kernver%.*}-ARCH"
  find "$srcdir" -name *.ko -exec cp {} "$pkgdir/usr/lib/modules/extramodules-${_kernver%.*}-ARCH" \;
  gzip -9 "$pkgdir/usr/lib/modules/extramodules-${_kernver%.*}-ARCH/"*
}
