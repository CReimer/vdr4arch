# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr
pkgver=1.7.30
pkgrel=2
pkgdesc=""
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libjpeg-turbo' 'perl' 'ttf-dejavu')
optdepends=('lirc: remote control support'
            'runvdr-extreme: startscript for vdr')
provides=("vdr-api=$pkgver")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf'
        'etc/logrotate.d/vdr')
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/Developer/${pkgname}-${pkgver}.tar.bz2"
        '60-vdr-tty.rules'
        'MainMenuHooks-v1_0_2.diff'
        'shutdown.sh'
        'shutdown-wrapper.c'
        'vdr.logrotate')
md5sums=('c6d75f2962bc3e22d9313c0ee4fa113a'
         '32dcab4ce46f8164977ab3abdeccec57'
         '301c9b9766ed5182b07f1debc79abc21'
         'ef8e11062f58a9eb4016dfbf66bb9044'
         '7cad811b4ac5ee6c0b5496d006f1e0ee'
         '4779390621b11766b94d6a60403b20d7')

build() {
  cd "${srcdir}"
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/MainMenuHooks-v1_0_2.diff

  ## Build core VDR with recommended optimization level
  CFLAGS=$(echo "$CFLAGS" | sed 's/-O2/-O3/g')
  CXXFLAGS=$(echo "$CXXFLAGS" | sed 's/-O2/-O3/g')

  make PREFIX="/usr" \
       LOCDIR="/usr/share/locale" \
       PLUGINLIBDIR="/usr/lib/vdr/plugins" \
       VIDEODIR="/srv/vdr/video" \
       CONFDIR="/var/lib/vdr" \
       CACHEDIR="/var/cache/vdr" \
       RESDIR="/usr/share/vdr" \
       VDR_USER="vdr" \
       all plugins
}

package() {
  cd "${srcdir}"
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  install -Dm644 60-vdr-tty.rules "$pkgdir/usr/lib/udev/rules.d/60-vdr-tty.rules"
  install -Dm644 vdr.logrotate "$pkgdir/etc/logrotate.d/vdr"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

