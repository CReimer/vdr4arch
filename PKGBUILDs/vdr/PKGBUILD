# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr
pkgver=1.7.29
pkgrel=1
pkgdesc=""
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fontconfig' 'libjpeg-turbo' 'perl' 'ttf-dejavu')
optdepends=('lirc: remote control support')
provides=("vdr-api=$pkgver")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf')
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/Developer/${pkgname}-${pkgver}.tar.bz2"
        '60-vdr-tty.rules'
        'av7110_ir_vdr_perms.conf'
        'MainMenuHooks-v1_0_2.diff'
        'setwakeup.function'
        'shutdown.sh'
        'shutdown-wrapper.c'
        'vdr-1.7.29-sortrectopmenu.diff')
md5sums=('a3f0ae42ba456aa1865c9ed065a64d80'
         '32dcab4ce46f8164977ab3abdeccec57'
         '180249e83f867e11a9c0c73e9b4805c8'
         '301c9b9766ed5182b07f1debc79abc21'
         '7cb3224f7b60d5afe8a5b1abaed61c40'
         '8d69ab549639fa41190cdf1a5bc898c8'
         '7cad811b4ac5ee6c0b5496d006f1e0ee'
         '7e2003b26201f8320c4a1585a52ae8a9')

build() {
  cd "${srcdir}"
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"

cat > Make.config <<EOF
PREFIX       = /usr
LOCDIR       = /usr/share/locale
PLUGINLIBDIR = /usr/lib/vdr/plugins
VIDEODIR     = /srv/vdr/video0
CONFDIR      = /var/lib/vdr
VDR_USER     = vdr
EOF

  patch -p1 -i ${srcdir}/MainMenuHooks-v1_0_2.diff
  patch -i ${srcdir}/vdr-1.7.29-sortrectopmenu.diff

  make plugins

  ## Build core VDR with recommended optimization level
  CFLAGS=$(echo "$CFLAGS" | sed 's/-O2/-O3/g')
  CXXFLAGS=$(echo "$CXXFLAGS" | sed 's/-O2/-O3/g')

  make
}

package() {
  cd "${srcdir}"
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"
  install -Dm644 setwakeup.function "$pkgdir/etc/rc.d/functions.d/setwakeup"
  install -Dm644 60-vdr-tty.rules "$pkgdir/usr/lib/udev/rules.d/60-vdr-tty.rules"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

