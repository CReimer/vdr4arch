# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=oscam
pkgver=7196
pkgrel=1
pkgdesc=""
url="http://www.oscam.to"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('bash' 'libusb' 'openssl')
optdepends=()
makedepends=('unzip')
conflicts=()
replaces=()
backup=('var/lib/oscam/oscam.ac'
        'var/lib/oscam/oscam.cacheex'
        'var/lib/oscam/oscam.cert'
        'var/lib/oscam/oscam.conf'
        'var/lib/oscam/oscam.dvbapi'
        'var/lib/oscam/oscam.guess'
        'var/lib/oscam/oscam.ird'
        'var/lib/oscam/oscam.provid'
        'var/lib/oscam/oscam.server'
        'var/lib/oscam/oscam.services'
        'var/lib/oscam/oscam.srvid'
        'var/lib/oscam/oscam.tiers'
        'var/lib/oscam/oscam.user'
        'var/lib/oscam/oscam.whitelist')
install=''
source=("${pkgname}-${pkgver}.zip::http://streamboard.de.vu:8001/changeset/${pkgver}/trunk?old_path=%2F&old=${pkgver}&format=zip"
        'rc.oscam')
noextract="${pkgname}-${pkgver}.zip"
md5sums=('aac1e300037c8b4b1c51314ef81a63c0'
         '3696ac9fa61a8eafbfd0b5bac68b735e')

build() {
  cd "$srcdir"
  unzip "${pkgname}-${pkgver}.zip" -x "/"

  cd "${srcdir}/trunk"
  chmod +x config.sh

  make CONF_DIR=/var/lib/oscam \
       USE_SSL=1 \
       USE_LIBUSB=1 \
       OSCAM_BIN=oscam \
       LIST_SMARGO_BIN=list_smargo
}

package() {
  cd "${srcdir}/trunk"

  #Install binaries
  install -Dm755 oscam "$pkgdir/usr/bin/oscam"
  install -Dm755 list_smargo "$pkgdir/usr/bin/list_smargo"

  #Install configs
  mkdir -p $pkgdir/var/lib/oscam
  install -Dm644 Distribution/doc/example/* "$pkgdir/var/lib/oscam"

  #Install man-pages
  mkdir -p $pkgdir/usr/share/man/man1/
  mkdir -p $pkgdir/usr/share/man/man5/
  install -Dm644 Distribution/doc/man/*.1 "$pkgdir/usr/share/man/man1"
  install -Dm644 Distribution/doc/man/*.5 "$pkgdir/usr/share/man/man5"

  #Install rc script
  install -Dm755 ${srcdir}/rc.oscam "$pkgdir/etc/rc.d/oscam"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
