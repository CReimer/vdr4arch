# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=graphlcd-base
pkgver=20120923
_gitver=6bbc07ed
pkgrel=2
pkgdesc="Project to support graphical LC displays connected to the PC"
url="http://projects.vdr-developer.org/projects/show/graphlcd"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fontconfig' 'gcc-libs')
optdepends='serdisplib: Add support for more serial displays'
backup=(etc/graphlcd.conf)
source=("http://projects.vdr-developer.org/git/$pkgname.git/snapshot/$pkgname-$_gitver.tar.bz2")
md5sums=('f029f146d08e76d7b017b9f816a21d17')

build() {
  cd "${srcdir}/${pkgname}-${_gitver}"
  sed -i '/CFLAGS/d' Make.config
  sed -i '/CXXFLAGS/d' Make.config
  sed -i '/LDFLAGS/d' Make.config
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_gitver}"
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  make DESTDIR="${pkgdir}" \
       BINDIR=${pkgdir}/usr/bin \
       LIBDIR=${pkgdir}/usr/lib \
       INCDIR=${pkgdir}/usr/include \
       MANDIR=${pkgdir}/usr/share/man \
       UDEVRULESDIR=${pkgdir}/usr/lib/udev/rules.d \
       install
  install -Dm644 graphlcd.conf "$pkgdir/etc/graphlcd.conf"
}
