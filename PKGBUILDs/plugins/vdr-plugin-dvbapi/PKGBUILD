# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr-plugin-dvbapi
pkgver=1.0.3
_vdrapi=1.7.31
pkgrel=2
pkgdesc="A bridge between VDR and OScam."
url="https://github.com/manio/vdr-plugin-dvbapi"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
source=("$pkgname-$pkgver.tar.bz2::https://github.com/manio/$pkgname/tarball/v$pkgver"
        "fix_connection_timed_out_error.diff::https://github.com/manio/vdr-plugin-dvbapi/commit/a98ff1e8.diff"
        "fix_segfault_on_exit.diff::https://github.com/manio/vdr-plugin-dvbapi/commit/24343f40.diff")
md5sums=('cf6ea2cc668d55ad94fb587cc787ee25'
         'd7aa9ffca09527ab1477913556498a63'
         '383aed9b58520f6a3d56bc48038b3e82')

package() {
  cd "${srcdir}/manio-${pkgname}-"*

  if [ "${CARCH}" = "x86_64" ]; then
    CPUOPT='x86-64'
    PARALLEL=PARALLEL_128_SSE2
  else
    CPUOPT="${CARCH}"
    PARALLEL=PARALLEL_32_INT
  fi

  patch -i $srcdir/fix_connection_timed_out_error.diff
  patch -i $srcdir/fix_segfault_on_exit.diff

  mkdir -p $pkgdir/usr/lib/vdr/plugins
  make CFLAGS="$(pkg-config vdr --variable=plugincflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=plugincflags)" \
       VDRDIR="$(pkg-config vdr --variable=includedir)/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=plugindir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=localedir)" \
       CPUOPT="$CPUOPT" \
       PARALLEL="$PARALLEL" \
       CSAFLAGS="$(pkg-config vdr --variable=plugincflags) -funroll-loops"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
