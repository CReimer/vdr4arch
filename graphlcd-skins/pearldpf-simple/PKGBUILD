# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=pearldpf-simple
pkgver=20120306
epoch=1
_gitver=e4fd6d7eb07d7952b4a4bd5d378837cda1bbb4d6
pkgrel=1
pkgdesc="Graphlcd skin for Pearl Digital Picture Frames"
arch=('any')
url="http://www.vdr-portal.de/board18-vdr-hardware/board11-displays/p1054042-pearl-display-yavdr-todo-install/#post1054042"
depends=('ttf-ubuntu-font-family' 'vdr-graphlcd')
makedepends=('git')
license=('GPL2')
source=("git+https://github.com/VDR4Arch/pearldpf-simple#commit=$_gitver")
backup=('usr/share/vdr/plugins/graphlcd/skins/pearldpf-simple/settings.conf')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git log -1 --pretty=format:%ad --date=short | sed 's/-//g'
}

prepare() {
  cd "$srcdir/$pkgname"
  sed -i 's/VDRSymbolsSans/DejaVuSans/g' $pkgname.skin
}

package() {
  mkdir -p "$pkgdir/usr/share/vdr/plugins/graphlcd/skins/$pkgname"
  cp -r "$srcdir/$pkgname/"{$pkgname.skin,icons,inc,locale,styles,settings.conf} \
    "$pkgdir/usr/share/vdr/plugins/graphlcd/skins/$pkgname"

  mkdir -p "$pkgdir/usr/share/vdr/plugins/graphlcd/fonts"
  cd "$pkgdir/usr/share/vdr/plugins/graphlcd/fonts"

  ln -s /usr/share/fonts/TTF/{Ubuntu-B.ttf,Ubuntu-C.ttf,UbuntuMono-B.ttf} .
}
